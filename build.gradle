import java.text.SimpleDateFormat

plugins {
    id 'com.github.ben-manes.versions' version '0.38.0'
    id 'java-library'
    id 'net.nemerosa.versioning' version '2.14.0'
    id 'org.shipkit.java' version '2.3.5'
}

group = 'co.ipregistry'

updateReleaseNotes.enabled = false
updateReleaseNotesOnGitHub.enabled = true

repositories {
    jcenter()
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}


jar {
    manifest {
        attributes(
                'Built-By': System.properties['user.name'],
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Name': 'co/ipregistry/api/client/',
                'Specification-Title': 'Ipregistry Client',
                'Specification-Version': project.version,
                'Specification-Vendor': 'Ipregistry',
                'Implementation-Title': 'co.ipregistry.api.client',
                'Implementation-Version': versioning.info.commit,
                'Implementation-Vendor': 'Ipregistry',
        )
    }
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    compileOnly 'org.projectlombok:lombok:1.18.20'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.2'
    implementation 'com.google.guava:guava:30.1.1-jre'
    implementation 'org.apache.httpcomponents.client5:httpclient5-fluent:5.0.3'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
    testImplementation 'org.mockito:mockito-core:3.8.0'

    integrationTestImplementation 'io.rest-assured:rest-assured:4.3.3'
    integrationTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    integrationTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
    samples {
        compileClasspath += main.output + main.compileClasspath
        runtimeClasspath += main.output + main.runtimeClasspath
    }
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
}

check.dependsOn integrationTest

test {
    useJUnitPlatform()
}

integrationTest {
    useJUnitPlatform()
}
